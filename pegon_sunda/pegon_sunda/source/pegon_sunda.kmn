store(&VERSION) '10.0'
store(&NAME) 'Pegon Sunda'
store(&COPYRIGHT) '© 2021 Ilham Nurwansah'

store(&KMW_RTL) '1'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'

begin Unicode > use(main)

c store(&mnemoniclayout) '1'

store(cons) \
  'ذ' 'ظ' 'خ' 'ح' 'ض' 'ص' 'ى' 'ط' 'م' 'ن' 'ب' 'ث' 'ش' 'ء' 'ز' 'ل' 'ك' 'ج' 'ه' 'غ' 'ف' \
  'د' 'س' 'ع' 'ي' 'ت' 'ر' 'و' 'ق' 'ࢴ' 'ڤ' 'ڄ' 'ڠ'

group(main) using keys
+ '-' > '-'
+ '=' > '='
+ [K_LBRKT]  > 'َي'
+ [SHIFT K_LBRKT]  > 'اَي'
+ [SHIFT K_RBRKT]  > '}'
+ [SHIFT K_BKSLASH]  > '|'
+ "'" > "'"
+ '"' > '"'
+ '.' > '.'
+ '`' > '`'
+ [SHIFT K_BKQUOTE] > U+007E
+ [SHIFT K_1] > U+0021
+ [SHIFT K_2] > "@"
+ [SHIFT K_3] > "#"
+ [SHIFT K_4] > "$"
+ [SHIFT K_6] > "^"
+ [SHIFT K_7] > "&"
+ [SHIFT K_9] > "("
+ [SHIFT K_0] > ")"
+ [SHIFT K_HYPHEN]  > "_"
+ [SHIFT K_EQUAL]   > "+"
+ [SHIFT K_COLON] > ':'
+ 'V' > 'ذ'
+ 'Z' > 'ظ'
+ 'K' > U+0762
+ 'H' > 'ه'
+ 'D' > 'ض'
+ 'S' > 'ص'
+ 'A' > 'اَ'
+ 'I' > 'اِ'
+ 'U' > 'اُ'
+ 'O' > 'اَو'
+ 'E' > 'اࣤ'
+ 'P' > 'ڤ'
+ 'Y' > 'ى'
+ 'T' > 'ط'
+ 'G' > 'ڠ'
+ 'J' > 'پ'
+ 'X' > 'ع'
+ 'm' > 'م'
+ 'n' > 'ن'
+ 'b' > 'ب'
+ 'v' > 'ث'
+ 'c' > 'ڄ'
+ 'C' > U+0683
+ 'x' > 'ء'
+ 'z' > 'ز'
+ 'l' > 'ل'
+ 'k' > 'ك'
+ 'j' > 'ج'
+ 'h' > 'ح'
+ 'g' > 'ࢴ'
+ 'f' > 'ف'
+ 'd' > 'د'
+ 's' > 'س'
+ 'a' > 'َ'
+ 'o' > 'َو'
+ 'i' > 'ِ'
+ 'u' > 'ُ'
+ 'y' > 'ي'
+ 't' > 'ت'
+ 'r' > 'ر'
+ 'e' > 'ࣤ'
+ 'w' > 'و'
+ 'q' > 'ق'
+ 'p' > 'ف'
+ 'Q' > U+062E
+ 'W' > dk(none)
+ 'R' > dk(none)
+ 'F' > dk(none)
+ 'L' > dk(none)
+ 'B' > dk(none)
+ 'N' > dk(none)
+ 'M' > dk(none)


'َ' + 'a' > U+064E U+0627
'َ' + 'A' > U+0627
'اَ' + 'a' > 'ئ'
'ِ' + 'i' > U+0650 U+0649
U+064F + 'u' > U+064F U+0648
U+064F + 'a' > U+064F U+0627
U+0684 + [K_RBRKT] > U+0683
U+0683 + [K_RBRKT] > U+0686
U+0686 + [K_RBRKT] > U+0684 
U+0633 + 'x' > U+0634
'أ' + 'A' > 'آ'
'ُ' + 'u' > 'ؤ'
'ِ' + 'e' > 'إ'
'ي' + 'x' > 'ئ'
'َ' + 'o' > 'ً'
'ُ' + 'o' > 'ٌ'
'ِ' + 'o' > 'ٍ'


c Rotations
  
store(a_rot1) 'َ' 'ا' 'آ' 'أ' 'إ' 'ى' 'ع'
store(a_rot2) 'ا' 'آ' 'أ' 'إ' 'ى' 'ع' 'َ'

store(d_rot1) 'د' 'ض'
store(d_rot2) 'ض' 'د'

store(h_rot1) 'ح' 'ه' 'خ'
store(h_rot2) 'ه' 'خ' 'ح'
                    
store(s_rot1) 'س' 'ص' 'ش'
store(s_rot2) 'ص' 'ش' 'س'

store(t_rot1) 'ت' 'ط' 'ة' 'ث' 'ظ' 'ذ'
store(t_rot2) 'ط' 'ة' 'ث' 'ظ' 'ذ' 'ت'

any(a_rot1) + '/' > index(a_rot2, 1)
any(d_rot1) + '/' > index(d_rot2, 1)
any(h_rot1) + '/' > index(h_rot2, 1)
any(s_rot1) + '/' > index(s_rot2, 1)
any(t_rot1) + '/' > index(t_rot2, 1)

c Punctuation

+ ';' > 'ْ'
+ '?' > '؟'
+ ',' > '،'
+ '%' > '٪'
                                               
store(digitkey) '0123456789'
store(digit) '٠١٢٣٤٥٦٧٨٩'

+ any(digitkey) > index(digit, 1)

any(digit) + '<' > index(digit, 1) '٬'
any(digit) + '>' > index(digit, 1) '٫'

+ '*' > '٭'

match > use(post)             

group(post)
                    
c Shadda
any(cons) index(cons, 1) > context(1) 'ّ'

c Allah
'ل' 'ّ' 'ه' > U+FDF2 
